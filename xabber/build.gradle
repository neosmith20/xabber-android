apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'kotlin-android'
apply plugin: 'com.frogermcs.androiddevmetrics'

android {
    compileSdkVersion 30
    buildToolsVersion '29.0.3'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1043
        versionName '3.0.1(1043)'
        vectorDrawables.useSupportLibrary = true
    }

    lintOptions {
        // problems with generated "applicationId" string resource
        disable 'MissingTranslation'
        disable 'InvalidPackage'

        abortOnError false
    }

    subprojects {
        afterEvaluate {
            if (getPlugins().hasPlugin('android') || getPlugins().hasPlugin('android-library')) {

                configure(android.lintOptions) {
                    abortOnError false

                }
            }
        }
    }

    flavorDimensions "destribution", "build"
    productFlavors {

        open {
            dimension "destribution"
            versionNameSuffix "-open"
        }

        store {
            dimension "destribution"
        }

        dev {
            dimension "build"
            applicationId "com.xabber.androiddev"
            resValue 'string', 'application_package', applicationId
            multiDexEnabled true
        }

        beta {
            dimension "build"
            applicationId "com.xabber.android.beta"
            resValue 'string', 'application_package', applicationId
            multiDexEnabled true
        }

        prod {
            dimension "build"
            applicationId "com.xabber.android"
            resValue 'string', 'application_package', applicationId
            multiDexEnabled true
        }

        vip {
            dimension "build"
            applicationId "com.xabber.androidvip"
            resValue 'string', 'application_package', applicationId
            multiDexEnabled true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dexOptions {
        javaMaxHeapSize "2g"
    }

    splits {
        abi {
            enable true
            reset()
            include 'armeabi', 'armeabi-v7a', 'arm64-v8a', 'mips', 'x86', 'x86_64'
            universalApk true
        }
    }

    testOptions {
        unitTests {
            unitTests.returnDefaultValues = true
            includeAndroidResources = true
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}

if (getGradle().getStartParameter().getTaskRequests().toString().contains("Store")) {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'
    firebaseCrashlytics {
        nativeSymbolUploadEnabled true
    }
}

ext {
    smackVersion = '4.2.1-SNAPSHOT'
}

dependencies {
    implementation project(path: 'MemorizingTrustManager', configuration: 'default')
    implementation project(path: 'emojicon', configuration: 'default')

    // smack
    implementation "org.igniterealtime.smack:smack-android-extensions:$smackVersion"
    implementation "org.igniterealtime.smack:smack-experimental:$smackVersion"
    implementation "org.igniterealtime.smack:smack-tcp:$smackVersion"
    implementation "org.igniterealtime.smack:smack-resolver-dnsjava:$smackVersion"

    // android support
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'androidx.browser:browser:1.3.0'
    implementation 'androidx.multidex:multidex:2.0.1'

    // firebase
    storeImplementation "com.google.firebase:firebase-messaging:21.1.0"
    storeImplementation 'com.google.firebase:firebase-crashlytics:17.4.1'
    storeImplementation 'com.google.firebase:firebase-analytics:18.0.3'
    storeImplementation 'com.google.firebase:firebase-crashlytics-ndk:17.4.1'

    // Opus codec
    implementation 'top.oply.opuslib:opuslib:1.0.2'

    // barcode scanner
    implementation 'com.journeyapps:zxing-android-embedded:3.6.0'

    // retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.1'
    implementation 'com.squareup.okhttp3:okhttp:4.9.1'

    // rx java
    implementation 'io.reactivex:rxandroid:1.1.0'
    implementation 'io.reactivex:rxjava:1.3.0'

    // social
    storeImplementation "com.twitter.sdk.android:twitter:3.0.0"

    // gms
    storeImplementation "com.google.android.gms:play-services-safetynet:17.0.1"
    storeImplementation "com.google.android.gms:play-services-auth:19.2.0"
    storeImplementation ("com.google.api-client:google-api-client-android:1.22.0") {
        exclude module: 'httpclient'
    }

    // event bus
    implementation 'org.greenrobot:eventbus:3.0.0'

    // commons-io
    implementation 'commons-io:commons-io:2.6'

    // bouncycastle
    implementation group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.58'

    // ui
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0' //Apache-2.0 License
    implementation 'me.relex:circleindicator:1.2.2@aar' //Apache-2.0 License
    implementation 'com.github.bumptech.glide:glide:4.9.0' //Apache-2.0 License
    implementation 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1' //MIT License
    implementation 'eu.davidea:flexible-adapter:5.1.0' //Apache-2.0 License
    implementation 'eu.davidea:flexible-adapter-ui:1.0.0' //Apache-2.0 License
    implementation 'eu.davidea:flexible-adapter-livedata:1.0.0-b3' //Apache-2.0 License
    compile 'io.realm:android-adapters:3.1.0'
    implementation 'com.soundcloud.android:android-crop:1.0.1@aar'//todo remove
    api 'com.theartofdev.edmodo:android-image-cropper:2.8.0' //Apache-2.0 License
    implementation 'de.hdodenhof:circleimageview:3.0.1' //Apache-2.0 License
    implementation "net.gcardone.junidecode:junidecode:0.4.1" //Apache-2.0 License

    // leak canary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

    // block canary
    debugImplementation 'com.github.markzhai:blockcanary-android:1.5.0'
    releaseImplementation 'com.github.markzhai:blockcanary-no-op:1.5.0'

    // test
    testImplementation 'junit:junit:4.12'
    testImplementation "org.robolectric:robolectric:4.3"
    testImplementation "org.robolectric:shadows-multidex:4.0.1"
    testImplementation "org.mockito:mockito-core:2.25.0"

    //kotlin
    implementation "androidx.core:core-ktx:1.6.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.1'

}

configurations {
    all*.exclude group: 'xpp3', module: 'xpp3'
    all*.exclude group: 'com.google.guava', module:'guava-jdk5'
}
